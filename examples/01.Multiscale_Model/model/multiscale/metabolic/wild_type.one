import 'protein_ribosomal.one'
import 'protein_non_ribosomal.one'

model WildType
  # Model of a wild-type E.coli. cell.

  input
    # Concentration of substrate in the bioreactor (grams/litre).
    s
    # Total host protein mass (fg/cell).
    m_h
  end

  p_r = ProteinRibosomal()

  rule
    p_r.nu := nu
    p_r.mu := mu
    p_r.r := r
    p_r.m_h := m_h
    p_r.J_host_sum := J_host_sum
  end

  p_nr = ProteinNonRibosomal()

  rule
    p_nr.nu := nu
    p_nr.mu := mu
    p_nr.r := r
    p_nr.m_h := m_h
    p_nr.J_host_sum := J_host_sum
  end

  parameter
    # Half-activation threshold of growth rate (grams/litre).
    K_s = 0.1802
    # Maximum effective translation rate per ribosome (aa/minute).
    nu_max = 1260
    # Average aminoacid mass (fg/aa).
    m_aa = 182.6e-9
    # Fraction of mature available ribosomes relative to the total (dimensionless).
    phi_t = 0.9473
  end

  species
    # Effective translation rate per ribosome (aa/minute).
    nu = 0
    # Specific cell growth rate (1/minute).
    mu = 0.01
    # Total protein mass calculate from the mass of individual proteins in the cell (fg/cell).
    m_p = 0
    # Total sum of resource recruitment strengths in the cell (dimensionless).
    J_sum = 0
    # Total sum of host resource recruitment strengths in the cell (dimensionless).
    J_host_sum = 0
    # Total sum of RRS in the cell taking into account E_m terms (dimensionless).
    J_sum_E = 0
    # Fraction of translating ribosomes from the mature ribosomes (dimensionless).
    phi_b_t = 0
    # Free mature ribosomes in the cell.
    r = 350
  end

  rule
    nu_eq:  nu := nu_max*s/(s + K_s) 
    mu == m_aa/m_h*nu*phi_b_t*phi_t*p_r.r_t
    m_p_eq:  m_p := p_r.m + p_nr.m
    J_sum_eq: J_sum := p_r.N*p_r.J + p_nr.N*p_nr.J
    J_host_sum := p_r.N*p_r.J + p_nr.N*p_nr.J
    J_sum_E_eq: J_sum_E := p_r.N*(1+1/p_r.E_m)*p_r.J + p_nr.N*(1+1/p_nr.E_m)*p_nr.J
    phi_b_t := J_host_sum/(1+J_sum_E)
    r == phi_t*p_r.r_t/(1+J_sum_E)
  end
end

standalone
  parameter __t_end = 1000

  cell = WildType()

  parameter
    s = 3.6
    m_h = 450
  end

  rule
    cell.s := s
    cell.m_h := m_h
  end
end
