### IMPORTS ###

# Cell model.
import './wild_type/wild_type.onemodel'
import './synthetic_circuit/one_protein.onemodel'
import './synthetic_circuit/antithetic_controller.onemodel'

# Mass equations.
import './mass_equation/mass_interpolated.onemodel'
import './mass_equation/mass_eq_jesus.onemodel'

# Bioreactors.
import './bioreactor/batch.onemodel'
import './bioreactor/continous.onemodel'
import './bioreactor/fedbatch.onemodel'

### META-MODEL OPTIONS ###

# Cell model: 'wild_type', 'one_protein', 'antithetic'.
cell_model = 'wild_type'

# Mass equation: 'interpolated', 'jesus', 'fix'.
mass_equation = 'interpolated'

# Bioreactor type: 'growth_fix', 'nu_fix', 'substrate_fix', 'batch', 'continous'.
bioreactor = 'nu_fix'

# Simulation time.
parameter __t_end = 2000


### CELL BURDEN MODEL ###

if strcmp(cell_model, 'wild_type')
  cell = WildType()
end

if strcmp(cell_model, 'one_protein')
  cell = OneProtein()
end

if strcmp(cell_model, 'antithetic')
  cell = AntitheticController()
end

### MASS EQUATION ###

if strcmp(mass_equation, 'interpolated')
  mass = MassInterpolated()

  rule 
    mass.mu := cell.mu
    cell.m_h := mass.m_h
  end
end

if strcmp(mass_equation, 'jesus')
  mass = MassEqJesus()

  rule 
    mass.mu := cell.mu
    cell.m_h := mass.m_h
  end
end

if strcmp(mass_equation, 'fix')
  rule cell.m_h := 450 #fg
end


### BIOREACTOR MODEL ###

if strcmp(bioreactor, 'growth_fix')
  rule
    # override nu equation in cell (aa/minute).
    cell.mu_equation:  cell.mu := 0.0231
  end
end

if strcmp(bioreactor, 'nu_fix')
  rule
    # override nu equation in cell (aa/minute).
    cell.nu_equation:  cell.nu_t := cell.nu
  end
end

if strcmp(bioreactor, 'substrate_fix')
  parameter
    s = 3.6
  end

  rule
    # Fix the substrate to a fix value (gram/litre).
    cell.s := s
  end
end

if strcmp(bioreactor, 'batch')
  bio = Batch()

  rule
    bio.mu := cell.mu
    bio.m_p := cell.m_p
    cell.s := bio.s
  end
end

if strcmp(bioreactor, 'continous')
  bio = Continous()

  rule
    bio.mu := cell.mu
    bio.m_p := cell.m_p
    cell.s := bio.s
  end
end

if strcmp(bioreactor, 'fedbatch')
  bio = Fedbatch()

  rule
    bio.mu := cell.mu
    bio.m_p := cell.m_p
    cell.s := bio.s
  end
end
